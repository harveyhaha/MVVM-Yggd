apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-platform-android'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion androidVersion.compileSdkVersion
    buildToolsVersion androidVersion.buildToolsVersion

    defaultConfig {
        applicationId "com.wtf.yggd"
        minSdkVersion androidVersion.minSdkVersion
        targetSdkVersion androidVersion.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        Properties properties = new Properties()
        properties.load(project.rootProject.file('public.properties').newDataInputStream())
        buildConfigField "String", "CLIENT_ID", properties.getProperty("CLIENT_ID")
        buildConfigField "String", "CLIENT_SECRET", properties.getProperty("CLIENT_SECRET")
        buildConfigField "String", "REDIRECT_URI", properties.getProperty("REDIRECT_URI")
    }
    androidExtensions {
        experimental = true
    }
    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "LoggingInterceptor", "true")
        }
        release {
            buildConfigField("boolean", "LoggingInterceptor", "false")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        pickFirst 'META-INF/kotlinx-coroutines-core.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':mvvm_yggd')

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.61"

    kapt libs.dagger2_compiler
    kapt libs.dagger2_android_processor
    kaptAndroidTest libs.dagger2_compiler
    kapt libs.room_compiler
    implementation libs.logging_interceptor
    implementation libs.circleimageview
}
